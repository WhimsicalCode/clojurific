;   Copyright (c) Rich Hickey. All rights reserved.
;   The use and distribution terms for this software are covered by the
;   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
;   which can be found in the file epl-v10.html at the root of this distribution.
;   By using this software in any fashion, you are agreeing to be bound by
;   the terms of this license.
;   You must not remove this notice, or any other, from this software.

(ns cljs.esm
  "Compiles ClojureScript to ES modules, one module per namespace, without
  the Google Closure Compiler. npm packages and relative JavaScript /
  TypeScript files required with string requires are emitted as plain
  imports, resolving and bundling is left to a bundler such as Vite."
  (:require [cljs.analyzer :as ana]
            [cljs.compiler :as comp]
            [cljs.env :as env]
            [cljs.js-deps :as deps]
            [cljs.util :as util]
            [cljs.vendor.clojure.data.json :as json]
            [clojure.edn :as edn]
            [clojure.java.io :as io]
            [clojure.string :as string])
  (:import [java.io File]
           [java.net URL]
           [java.util.concurrent Executors Future LinkedBlockingQueue]
           [java.util.concurrent.locks ReentrantLock]))

(def default-opts
  {:module-format  :esm
   :optimizations  :none
   :output-dir     "out"
   :language-out   :es2020
   :static-fns     true
   :cache-analysis true
   :source-map     true})

(def goog-shims
  "The subset of the Closure Library cljs.core and friends depend on,
  reimplemented as ES modules. Paths are relative to the output directory and
  the cljs/esm resource directory."
  ["goog.js"
   "goog/array.js"
   "goog/math.js"
   "goog/object.js"
   "goog/string.js"
   "goog/userAgent/product.js"])

(def ^:dynamic *generated-sources*
  "Generated namespaces of the build, {ns File}, see :test-runner."
  {})

(defn- source-for-ns
  "Returns the URL of the ClojureScript source of namespace ns on the
  classpath, nil if ns isn't a ClojureScript namespace."
  ^URL [ns]
  (if-let [^File f (get *generated-sources* ns)]
    (.toURL (.toURI f))
    (util/ns->source ns)))

(defn- jar-url? [^URL url]
  (= "jar" (.getProtocol url)))

(defn- source-file
  "Returns a File for a source URL. Sources in jars are copied to the output
  directory, like cljs.closure does."
  ^File [^URL url ns output-dir]
  (if (jar-url? url)
    (let [ext  (util/ext url)
          file (io/file output-dir (util/ns->relpath ns ext))]
      (when (or (not (.exists file))
                (< (.lastModified file) (util/last-modified url)))
        (util/mkdirs file)
        (with-open [in (io/input-stream url)]
          (io/copy in file)))
      file)
    (io/file url)))

(def ^:private parse-ns-cache (atom {}))

(defn- parse-ns
  "ana/parse-ns, cached by file and modification time, the watcher finds
  sources on every change."
  [^File file]
  (let [k [(.getPath file) (.lastModified file)]]
    (or (get @parse-ns-cache k)
        (let [ns-info (ana/parse-ns file)]
          (swap! parse-ns-cache assoc k ns-info)
          ns-info))))

(defn- main-namespaces
  "The :main namespaces, a symbol or a collection of them, and :extra-main's,
  which tools add to them (the Vite plugin, the namespaces of the pages'
  scripts)."
  [{:keys [main extra-main]}]
  (distinct (concat (cond (coll? main) main main [main]) extra-main)))

(defn- js-entry-namespaces
  "The namespaces of the vars :js-entries export."
  [{:keys [js-entries]}]
  (->> (vals js-entries) (mapcat (comp vals :exports)) (map (comp symbol namespace)) distinct))

(defn- mains
  "The namespaces a build compiles with their dependencies: the :preloads,
  the main namespaces, those :js-entries export from, and under :esm-repl
  the REPL runtime and the namespaces required at the REPL (::repl-mains, an
  atom)."
  [{:keys [preloads esm-repl ::repl-mains] :as opts}]
  (concat (when esm-repl [comp/esm-repl-runtime])
          preloads (main-namespaces opts) (js-entry-namespaces opts)
          (some-> repl-mains deref sort)))

(defn find-sources
  "Returns the parsed ns info of namespaces and their transitive ClojureScript
  dependencies in dependency order."
  [namespaces {:keys [output-dir]}]
  (letfn [(visit [{:keys [seen order] :as state} ns]
            (if (contains? seen ns)
              state
              (if-let [url (source-for-ns ns)]
                (let [file    (source-file url ns output-dir)
                      ns-info (parse-ns file)
                      state   (reduce visit
                                (update state :seen conj ns)
                                (->> (:requires ns-info)
                                     (remove string?)
                                     (map symbol)
                                     (cons 'cljs.core)
                                     (remove #{ns})))]
                  (update state :order conj (assoc ns-info :source-file file)))
                (update state :seen conj ns))))]
    (:order (reduce visit {:seen #{} :order []} (map symbol namespaces)))))

(defn- output-file ^File [ns {:keys [output-dir]}]
  (io/file output-dir (util/ns->relpath ns :js)))

(defn- install-goog-shims [{:keys [output-dir]}]
  (doseq [path goog-shims]
    (let [res  (io/resource (str "cljs/esm/" path))
          dest (io/file output-dir path)]
      (when (or (not (.exists dest))
                (not= (slurp res) (slurp dest)))
        (util/mkdirs dest)
        (spit dest (slurp res))))))

(defn- spit-if-changed [^File f content]
  (when (or (not (.exists f)) (not= content (slurp f)))
    (util/mkdirs f)
    (spit f content)))

(defn- relative-import
  "Import path of file to from file from, both relative to the output dir."
  [from to]
  (let [rel (-> (.relativize (.toPath (.getParentFile (io/file "/" from))) (.toPath (io/file "/" to)))
                str
                (string/replace File/separator "/"))]
    (if (string/starts-with? rel ".") rel (str "./" rel))))

(def ^:private goog-base "goog-lib/goog/base.js")

(defn- indirect-eval
  "Makes Closure Library's direct eval calls (goog.json.parse, base.js'
  module loader) indirect: a direct eval keeps minifiers from renaming any
  binding it can see, i.e. the top level of the chunk it's bundled into."
  [src]
  (string/replace src #"(?<![\w$.])eval\(" "(0, eval)("))

(defn- goog-base-module
  "Closure Library's base.js as an ES module exporting goog, also installed as
  the global goog Closure Library code expects. The debug loader is disabled,
  dependencies are loaded by imports."
  []
  (let [src (slurp (io/resource "goog/base.js"))]
    (str "// Closure Library base.js as an ES module, see cljs.esm/install-goog-libs\n"
         "globalThis.CLOSURE_NO_DEPS = true;\n"
         "globalThis.CLOSURE_DEFINES = Object.assign({'goog.ENABLE_DEBUG_LOADER': false}, globalThis.CLOSURE_DEFINES);\n"
         (-> src
             indirect-eval
             (string/replace #"(?m)^var COMPILED = false;" "var COMPILED = false; // eslint-disable-line")
             (string/replace #"(?m)^var goog = goog \|\| \{\};" "var goog = {};")
             (string/replace #"(?m)^goog\.global =\s*[^;]*;" "goog.global = globalThis;"))
         ;; base.js isn't strict mode, ES modules are: uids of non-extensible
         ;; objects can't be properties, see cljs/esm/goog.js
         "\nconst frozenUids = new WeakMap();\n"
         "const getUid = goog.getUid;\n"
         "goog.getUid = function(obj) {\n"
         "  if (Object.isExtensible(obj) || Object.prototype.hasOwnProperty.call(obj, goog.UID_PROPERTY_)) return getUid(obj);\n"
         "  let uid = frozenUids.get(obj);\n"
         "  if (uid === undefined) frozenUids.set(obj, (uid = ++goog.uidCounter_));\n"
         "  return uid;\n"
         "};\n"
         "if (goog.getHashCode) goog.getHashCode = goog.getUid;\n"
         "\nglobalThis.goog = goog;\nexport { goog };\n")))

(defn- goog-file-module
  "A Closure Library file as an ES module importing the files providing its
  requires, goog.module files are run by goog.loadModule."
  [lib {:keys [file requires module]}]
  (let [src     (indirect-eval (slurp (io/resource file)))
        out     (str "goog-lib/" file)
        imports (->> requires
                     (keep #(:file (lib %)))
                     (remove #{file "goog/base.js"})
                     distinct)]
    (str "import { goog } from \"" (relative-import out goog-base) "\";\n"
         (apply str (map #(str "import \"" (relative-import out (str "goog-lib/" %)) "\";\n") imports))
         (if (= :goog module)
           (str "goog.loadModule(function(exports) {'use strict';\n" src "\n;return exports;});\n")
           src))))

(defn install-goog-libs
  "Writes the Closure Library namespaces used without an ES module shim
  (recorded by the compiler as :cljs.esm/goog-libs of the namespaces using
  them) and the files they depend on as ES modules run by Closure Library's
  base.js. goog-lib/ns/<ns>.js default exports namespace ns."
  [compiler-env {:keys [output-dir]}]
  (let [idx  (:js-dependency-index @compiler-env)
        lib  #(deps/closure-lib idx %)
        nses (into (sorted-set) (mapcat ::goog-libs) (vals (::ana/namespaces @compiler-env)))]
    (when (seq nses)
      (let [files (loop [queue (vec (keep lib nses)) seen {}]
                    (if-let [{:keys [file requires] :as info} (first queue)]
                      (if (or (contains? seen file) (= "goog/base.js" file))
                        (recur (subvec queue 1) seen)
                        (recur (into (subvec queue 1) (keep lib requires))
                          (assoc seen file info)))
                      (vals seen)))]
        (spit-if-changed (io/file output-dir goog-base) (goog-base-module))
        (doseq [info files]
          (spit-if-changed (io/file output-dir "goog-lib" (:file info)) (goog-file-module lib info)))
        (doseq [ns nses]
          (when-let [{:keys [file]} (lib ns)]
            (let [out (comp/esm-goog-lib-path ns)]
              (spit-if-changed (io/file output-dir out)
                (str "import { goog } from \"" (relative-import out goog-base) "\";\n"
                     (when-not (= "goog/base.js" file)
                       (str "import \"" (relative-import out (str "goog-lib/" file)) "\";\n"))
                     "export default goog.module.get(\"" ns "\");\n")))))))))

(defn- write-package-json
  "Marks the output directory as ES modules for Node.js."
  [{:keys [output-dir]}]
  (let [f (io/file output-dir "package.json")]
    (when-not (.exists f)
      (util/mkdirs f)
      (spit f (json/write-str {:type "module"})))))

(defn- js-entry-path [entry]
  (str "cljs-esm-entries/" (name entry) ".js"))

(defn- write-js-entries
  "Writes the :js-entries modules, {name {:exports {js-name ns/var}}}: each
  exports vars of the build under JavaScript names, like shadow-cljs'
  :exports. The namespaces are compiled, the entries are bundle entry points,
  importing the :preloads first."
  [{:keys [output-dir js-entries preloads]}]
  (doseq [[entry {:keys [exports]}] js-entries
          :let [path (js-entry-path entry)]]
    (spit-if-changed (io/file output-dir path)
      (apply str
        (concat
          (for [preload preloads]
            (str "import \"" (relative-import path (util/ns->relpath (symbol preload) :js)) "\";\n"))
          (for [[js-name sym] (sort-by (comp name key) exports)]
          (str "export { " (comp/esm-var-name (name sym)) " as " (name js-name) " } from \""
               (relative-import path (util/ns->relpath (symbol (namespace sym)) :js)) "\";\n")))))))

(defn- check-js-entries
  "Throws when a :js-entries export names a var the build doesn't define."
  [compiler-env {:keys [js-entries]}]
  (doseq [[entry {:keys [exports]}] js-entries
          [js-name sym] exports
          :when (not (get-in @compiler-env [::ana/namespaces (symbol (namespace sym)) :defs (symbol (name sym))]))]
    (throw (ex-info (str "js-entries " (name entry) " exports " (name js-name) ", undefined var " sym)
             {:entry entry :export js-name :var sym}))))

(defn- write-build-info
  "Writes cljs-esm.json to the output directory, the build's entry points for
  bundlers: the modules of the main namespaces and the :js-entries."
  [{:keys [output-dir js-entries] :as opts}]
  (write-js-entries opts)
  (spit-if-changed (io/file output-dir "cljs-esm.json")
    (json/write-str
      {:main (into (sorted-map)
               (map (fn [ns] [(str ns) (util/ns->relpath (symbol ns) :js)]))
               (main-namespaces opts))
       :entries (into (sorted-map)
                  (map (fn [entry] [(name entry) (js-entry-path entry)]))
                  (keys js-entries))
       :mode (some-> (:mode opts) name)})))

(defn- normalize-closure-defines
  "Normalizes :closure-defines keys to the munged names goog-define vars are
  looked up by."
  [defines]
  (into {}
    (map (fn [[k v]]
           [(if (or (symbol? k) (keyword? k))
              (str (comp/munge (symbol (namespace k) (name k))))
              k)
            v]))
    defines))

(defn- ns-api
  "The parts of a namespace's analysis its dependents' compiled output
  depends on: its vars (cljs.test's run-tests lists a namespace's tests) and
  arities of fns invoked directly under :static-fns."
  [ns-analysis]
  (into {}
    (map (fn [[sym v]]
           [sym (-> (select-keys v [:fn-var :variadic? :max-fixed-arity :dynamic
                                    :protocol-symbol :protocol])
                    ;; arities, param names differ between compiles (gensyms)
                    (assoc :arities (map count (:method-params v))))]))
    (:defs ns-analysis)))

(defn- api [compiler-env ns]
  (ns-api (get-in @compiler-env [::ana/namespaces ns])))

(defn- cached-analysis
  "A namespace's analysis from the analysis cache of its last compile, nil
  without one."
  [source-file {:keys [output-dir]}]
  (let [f (ana/cache-file source-file (ana/parse-ns source-file) output-dir)]
    ;; cljs.core's can be a resource of the compiler's
    (when (and (instance? File f) (.exists ^File f))
      (case (util/ext f)
        "edn"  (edn/read-string (slurp f))
        "json" (let [{:keys [reader read]} @ana/transit]
                 (with-open [in (io/input-stream f)]
                   (read (reader in :json ana/transit-read-opts))))))))

(def ^:private ^:dynamic *api-changed*
  "Namespaces of a build recompiled with a different api, an atom: the
  namespaces requiring them are recompiled too, their output may depend on
  it. Like cljs.closure's :recompile-dependents, without recompiling the
  dependents of every namespace recompiled."
  nil)

(defn- missing-preloads?
  "Whether the output of namespace ns lacks the imports of the :preloads (and
  the REPL runtime) it needs as a main namespace, compiled before it was one,
  i.e. before a page loaded it."
  [ns ^File dest opts]
  (boolean
    (when-let [preloads (and (.exists dest) (seq (comp/esm-preloads ns opts)))]
      (let [out (slurp dest)]
        (not-every? #(string/includes? out (str "import \"" (comp/esm-ns-path ns %) "\";")) preloads)))))

(defn- compile-ns
  "Compiles a namespace if its output isn't up to date, or when a namespace
  it requires was recompiled with a different api. Up to date namespaces
  aren't analyzed by compile-file: reads their analysis (from the analysis
  cache), or each of their dependents, compiled in parallel, would analyze
  them concurrently."
  [{:keys [ns source-file requires]} opts]
  (let [dest   (output-file ns opts)
        force? (or (and *api-changed* (some @*api-changed* (map symbol (remove string? requires))))
                   (missing-preloads? ns dest opts))
        opts   (cond-> opts force? (assoc :force true))
        stale? (and *api-changed*
                    (or (:force opts) (comp/requires-compilation? source-file dest opts)))
        before (when stale? (ns-api (cached-analysis source-file opts)))
        ret    (comp/compile-file source-file dest opts)]
    (when-not (get-in @env/*compiler* [::ana/namespaces ns :defs])
      (if (= 'cljs.core ns)
        (comp/with-core-cljs opts (fn []))
        (ana/analyze-file source-file opts)))
    (when (and stale? (not= before (api env/*compiler* ns)))
      (swap! *api-changed* conj ns))
    ret))

(defn- compile-parallel
  "Compiles inputs (in dependency order) with a pool of threads, a namespace
  is compiled once all its dependencies have been."
  [inputs opts]
  (let [threads (or (:parallel-build-threads opts)
                    (.availableProcessors (Runtime/getRuntime)))
        pool    (Executors/newFixedThreadPool threads)
        futures (atom {})]
    (try
      (doseq [{:keys [ns requires] :as input} inputs]
        (let [deps (keep #(get @futures (symbol %)) (remove string? requires))
              f    (.submit pool
                     ^Callable
                     (bound-fn []
                       (doseq [^Future d deps] (.get d))
                       (compile-ns input opts)))]
          (swap! futures assoc ns f)))
      (doseq [^Future f (vals @futures)]
        (.get f))
      (finally
        (.shutdown pool)))))

(defn- ensure-analyzed!
  "Namespaces up to date aren't recompiled, nor analyzed. Reads their
  analysis (from the analysis cache) so that the compiler environment
  describes the whole build, for build hooks and the watcher."
  [compiler-env inputs opts]
  (doseq [{:keys [ns source-file]} inputs]
    (when-not (get-in @compiler-env [::ana/namespaces ns :name])
      (ana/analyze-file source-file opts))))

(defn- run-hooks!
  "Calls the :build-hooks, [fn-sym & args], with the build and args. The
  build is a map of :compiler-env (a value), :namespaces (in dependency
  order), :mode, :options, :trigger and :changed-namespaces. trigger is
  :build, :watch (a recompile) or :repl-eval (a form evaluated at the REPL
  changed the analysis of the changed namespaces)."
  ([compiler-env inputs opts]
   (run-hooks! compiler-env inputs opts :build (map :ns inputs)))
  ([compiler-env inputs opts trigger changed]
   (doseq [[f & args] (:build-hooks opts)]
     (apply (requiring-resolve f)
       {:compiler-env       @compiler-env
        :namespaces         (mapv :ns inputs)
        :mode               (:mode opts)
        :options            opts
        :trigger            trigger
        :changed-namespaces (vec changed)}
       args))))

(defn- cljs-warnings
  "The analyzer's warnings with the :warnings option applied, like
  cljs.closure: a map of warning types to true, false, :warning, :error or
  :off, or one of them for the undeclared var and namespace warnings.
  :warnings-as-errors true makes every enabled warning an error, like
  shadow-cljs. Closure namespace munging doesn't happen under ES module
  output."
  [{:keys [warnings warnings-as-errors] :or {warnings true}}]
  (-> ana/*cljs-warnings*
      (merge (if (map? warnings)
               warnings
               (zipmap [:unprovided :undeclared-var :undeclared-ns :undeclared-ns-form]
                 (repeat (if (keyword? warnings) warnings (boolean warnings))))))
      (assoc :munged-namespace false)
      (cond-> warnings-as-errors
        (as-> ws (reduce-kv (fn [m k v] (assoc m k (if (#{true :warning} v) :error v))) ws ws)))))

(defn compiler-bindings
  "The dynamic bindings compiling with opts, as cljs.closure binds them:
  :warnings, :elide-asserts and :load-tests."
  [opts]
  {#'ana/*cljs-warnings* (cljs-warnings opts)
   #'*assert*            (not= true (:elide-asserts opts))
   #'ana/*load-tests*    (not= false (:load-tests opts))})

(defn- prepare-opts [opts]
  (-> (merge default-opts opts)
      (update :closure-defines normalize-closure-defines)
      ;; as cljs.closure: keyword and symbol constants are hoisted into a
      ;; constants module (see write-constants!)
      (cond-> (true? (:optimize-constants opts)) (assoc :emit-constants true))
      ;; the generated test runner is a main namespace
      (cond-> (:test-runner opts)
        (update :main #(vec (distinct (conj (if (coll? %) (vec %) (if % [%] [])) (-> opts :test-runner :ns))))))))

(declare watch-dirs source-files)

(defn- write-constants!
  "With :emit-constants (:optimize-constants), writes the constants module the
  compiled namespaces import their keyword and symbol constants from:
  cljs/core/constants.js, an export per constant."
  [compiler-env opts]
  (when (:emit-constants opts)
    (spit-if-changed (io/file (:output-dir opts) (util/ns->relpath ana/constants-ns-sym :js))
      (with-out-str
        (comp/emit-esm-constants-table (::ana/constant-table @compiler-env))))))

(defn- test-namespaces
  "The ClojureScript namespaces in the source directories matching regexp,
  sorted."
  [regexp opts]
  (->> (source-files (watch-dirs opts))
       keys
       (filter #(re-find #"\.clj[sc]$" %))
       (keep #(try (:ns (parse-ns (io/file %))) (catch Throwable _ nil)))
       (filter #(re-find (re-pattern regexp) (str %)))
       distinct
       sort))

(defn- test-runner-source
  "The file the :test-runner namespace is generated into, nil without one."
  ^File [{{:keys [ns]} :test-runner :keys [output-dir]}]
  (when ns
    (io/file output-dir "cljs-esm-gen" (util/ns->relpath ns :cljs))))

(defn- slurp-if-exists [^File f]
  (when (.exists f) (slurp f)))

(defn- generate-test-runner
  "Writes the :test-runner namespace, requiring the test namespaces matching
  :ns-regexp (default -test$) and calling (runner run-tests) with a fn running
  them, given a cljs.test env. Returns {ns File}."
  [{{:keys [ns ns-regexp runner] :or {ns-regexp "-test$"}} :test-runner :as opts}]
  (let [nses (test-namespaces ns-regexp opts)
        file (test-runner-source opts)
        env  (gensym "env")]
    (util/mkdirs file)
    (spit-if-changed file
      (with-out-str
        (prn (list* 'ns ns
               "Generated by cljs.esm (:test-runner), runs the test namespaces."
               [(list* :require ['cljs.test] [(symbol (namespace runner))] (map vector nses))]))
        (prn (list runner
               (list 'fn [env]
                 (list* 'cljs.test/run-tests env (map #(list 'quote %) nses)))))))
    {ns file}))

(defn- check-classpath!
  "Fails when another ClojureScript compiler is on the classpath, usually
  org.clojure/clojurescript brought in by a library: the classpath order
  decides whose namespaces load, and its jar's precompiled classes are loaded
  instead of these sources."
  []
  (let [loader (.getContextClassLoader (Thread/currentThread))
        dupes  (keep (fn [path]
                       (let [urls (distinct (map str (enumeration-seq (.getResources loader path))))]
                         (when (next urls)
                           [path urls])))
                 ["cljs/analyzer.cljc" "cljs/core.cljs"])]
    (when (seq dupes)
      (throw (ex-info (str "More than one ClojureScript compiler is on the classpath:\n"
                        (apply str (for [[path urls] dupes]
                                     (apply str "  " path "\n" (map #(str "    " % "\n") urls))))
                        "Exclude org.clojure/clojurescript from the library that brings it in"
                        " (clojure -X:deps tree shows which one) with :exclusions"
                        " [org.clojure/clojurescript], or from the whole dependency tree with"
                        " :override-deps, replacing it with the empty project in this"
                        " compiler's no-clojurescript directory.")
               {:resources (into {} dupes)})))))

(defn build
  "Compiles the namespaces in :main (a symbol or a collection of symbols) and
  their dependencies to ES modules in :output-dir. Returns the compiled
  namespaces in dependency order.

  :test-runner {:ns my.test-runner :ns-regexp \"-test$\" :runner my.test/start}
  generates (and compiles) namespace my.test-runner, requiring the test
  namespaces of the source directories, calling (my.test/start run-tests)."
  ([opts]
   (check-classpath!)
   (build opts (env/default-compiler-env (merge default-opts opts))))
  ([opts compiler-env]
   (let [opts (prepare-opts opts)]
     (with-bindings (assoc (compiler-bindings opts)
                      #'*generated-sources* (if (:test-runner opts)
                                              (generate-test-runner opts)
                                              *generated-sources*)
                      #'*api-changed* (atom #{}))
       (env/with-compiler-env compiler-env
         (swap! compiler-env assoc :options opts)
         (let [start  (System/nanoTime)
               inputs (find-sources (mains opts) opts)]
           ;; lets the compiler know which namespaces are part of the build
           ;; before they're analyzed
           (swap! compiler-env assoc ::namespaces (into #{} (map :ns) inputs))
           (swap! compiler-env update ::ana/data-readers merge (ana/load-data-readers))
           (write-build-info opts)
           ;; cljs.core is analyzed first, the compiler expects its analysis
           ;; to be available when compiling anything else
           (compile-ns (first (filter #(= 'cljs.core (:ns %)) inputs)) opts)
           (let [others (remove #(= 'cljs.core (:ns %)) inputs)]
             (if (:parallel-build opts)
               (compile-parallel others opts)
               (doseq [input others]
                 (compile-ns input opts))))
           (check-js-entries compiler-env opts)
           (write-constants! compiler-env opts)
           (install-goog-shims opts)
           (install-goog-libs compiler-env opts)
           (write-package-json opts)
           (when (seq (:build-hooks opts))
             (ensure-analyzed! compiler-env inputs opts)
             (run-hooks! compiler-env inputs opts))
           (when (:verbose opts)
             (util/debug-prn
               (format "Compiled %d namespaces in %.1fs" (count inputs)
                 (/ (- (System/nanoTime) start) 1e9))))
           (map :ns inputs)))))))

(def ^:private stdout
  "The process' standard output, events go there from any thread, i.e. the
  REPL's, where *out* is a REPL session's."
  (delay (.getRawRoot #'*out*)))

(def ^:private stderr
  (delay (.getRawRoot #'*err*)))

(defn event!
  "Prints a build event as a JSON line, consumed by the Vite plugin."
  [type m]
  (let [^java.io.Writer out @stdout]
    (locking out
      (.write out (str "[cljs.esm] " (json/write-str (assoc m :type type)) "\n"))
      (.flush out))))

(defn- collecting-warnings
  "The warning handlers, collecting the warnings printed into the atom
  warnings as {:message :file :line :column :output}, :output is the
  namespace's output file, for the watcher's compiled events."
  [warnings opts]
  (conj ana/*cljs-warning-handlers*
    (fn [warning-type env extra]
      (when (#{true :warning} (warning-type ana/*cljs-warnings*))
        (when-let [s (ana/error-message warning-type extra)]
          (swap! warnings conj {:message s
                                :file    (some-> ana/*cljs-file* str)
                                :line    (:line env)
                                :column  (:column env)
                                :output  (when-let [ns ana/*cljs-ns*] (.getPath (output-file ns opts)))}))))))

(defn- error-data [^Throwable e]
  (let [data  (ex-data (or (ex-cause e) e))
        cause (loop [e e] (if-let [c (ex-cause e)] (recur c) e))]
    {:message (.getMessage cause)
     :file    (or (:file data) (:clojure.error/source data))
     :line    (or (:line data) (:clojure.error/line data))
     :column  (or (:column data) (:clojure.error/column data))}))

(defn- source-files
  "All ClojureScript and Clojure (macro) files in dirs with their
  modification time."
  [dirs]
  (into {}
    (comp
      (mapcat #(file-seq (io/file %)))
      (filter #(.isFile ^File %))
      (filter #(re-find #"\.(cljs|cljc|clj)$" (.getName ^File %)))
      (map (fn [^File f] [(.getCanonicalPath f) (.lastModified f)])))
    dirs))

(defn- dependents
  "Namespaces of the build directly requiring any of nses."
  [compiler-env nses]
  (let [nses (set nses)]
    (->> (vals (::ana/namespaces @compiler-env))
         (filter (fn [{:keys [requires uses]}]
                   (some nses (concat (vals requires) (vals uses)))))
         (map :name))))

(defn- macro-dependents
  "Namespaces of the build using macros of any of macro-nses."
  [compiler-env macro-nses]
  (let [macro-nses (set macro-nses)]
    (->> (vals (::ana/namespaces @compiler-env))
         (filter (fn [{:keys [require-macros use-macros]}]
                   (some macro-nses (concat (vals require-macros) (vals use-macros)))))
         (map :name))))

(defn- file-ns
  "The namespace declared by a Clojure or ClojureScript file."
  [^File f]
  (try
    (:ns (ana/parse-ns f))
    (catch Throwable _ nil)))

(defn- recompile!
  "Recompiles namespaces nses (and their dependents if their API changed),
  returns the namespaces compiled."
  [compiler-env inputs nses opts]
  (let [by-ns (into {} (map (juxt :ns identity)) inputs)]
    (loop [queue (vec nses) done #{}]
      (if-let [ns (first queue)]
        (if (or (contains? done ns) (nil? (by-ns ns)))
          (recur (subvec queue 1) done)
          (let [before (api compiler-env ns)]
            (compile-ns (by-ns ns) (assoc opts :force true))
            (let [changed? (not= before (api compiler-env ns))]
              (recur (cond-> (subvec queue 1)
                       changed? (into (dependents compiler-env [ns])))
                (conj done ns)))))
        done))))

(defn- resource-refs
  "Classpath resources namespaces depend on, {path #{ns}}. Macros record
  them as {path last-modified} under :cljs.esm/resource-refs of the
  namespace being compiled, see watch-resource!."
  [compiler-env]
  (reduce-kv
    (fn [m ns {:keys [::resource-refs]}]
      (reduce (fn [m path] (update m path (fnil conj #{}) ns)) m (keys resource-refs)))
    {}
    (::ana/namespaces @compiler-env)))

(defn- resource-mtimes [refs]
  (into {}
    (keep (fn [path]
            (when-let [res (io/resource path)]
              [path (util/last-modified res)])))
    (keys refs)))

(defn watch-resource!
  "For macros: records that the namespace being compiled depends on the
  classpath resource at path, the watcher recompiles the namespace when the
  resource changes."
  [env path]
  (when env/*compiler*
    (let [res (or (io/resource path)
                  (throw (ana/error env (str "Resource not found: " path))))]
      (swap! env/*compiler* assoc-in
        [::ana/namespaces (-> env :ns :name) ::resource-refs path]
        (util/last-modified res)))))

(defn- resource-root
  "The classpath directory resource path is in, nil if it isn't in one."
  [path]
  (when-let [res (io/resource path)]
    (when (= "file" (.getProtocol ^URL res))
      (let [file (.getCanonicalPath (io/file res))]
        (subs file 0 (- (count file) (count path) 1))))))

(defn- watch-dirs
  "Directories with sources to watch, :watch-dirs or the classpath
  directories, without the output directory and the compiler's own sources:
  a changed compiler needs a new watcher, reloading it in place doesn't
  work."
  [{:keys [output-dir] :as opts}]
  (let [output   (.getCanonicalPath (io/file output-dir))
        compiler (set (keep resource-root ["cljs/compiler.cljc" "cljs/core.cljs"]))]
    (->> (or (:watch-dirs opts)
             (string/split (System/getProperty "java.class.path") #":"))
         (map io/file)
         (filter #(.isDirectory ^File %))
         (map #(.getCanonicalPath ^File %))
         (remove #(or (= output %) (string/starts-with? output (str % File/separator))))
         (remove compiler)
         distinct)))

(defn- start-stdin-reader!
  "Under :exit-with-parent, reads the parent's messages from stdin, puts
  the paths of `changed <path>` lines on queue, passes `repl <json>` lines
  to the REPL (cljs.esm.repl), exits on EOF: the parent closes stdin when it
  exits, the watcher has to stop too."
  [^LinkedBlockingQueue queue]
  (doto (Thread. (fn []
                   (with-open [rdr (io/reader System/in)]
                     (doseq [^String line (line-seq rdr)]
                       (cond
                         (string/starts-with? line "changed ")
                         (.put queue (subs line 8))

                         (string/starts-with? line "repl ")
                         (try
                           ((requiring-resolve 'cljs.esm.repl/handle-message!) (subs line 5))
                           (catch Throwable e
                             (binding [*out* *err*]
                               (println "cljs.esm.repl:" (.getMessage e))))))))
                   (System/exit 0)))
    (.setDaemon true)
    (.start)))

(defn- poll-changes
  "Polls dirs for changed source files, returns [changed files']. Removed
  files are changes too: a generated test runner requires what is left."
  [dirs files]
  (Thread/sleep 100)
  (let [files' (source-files dirs)]
    [(concat
       (keep (fn [[path mtime]]
               (when (not= mtime (get files path)) (io/file path)))
         files')
       (keep (fn [path]
               (when-not (contains? files' path) (io/file path)))
         (keys files)))
     files']))

(defn- next-changes
  "Blocks until the parent reports changed files (sources or resources),
  returns them, batching the changes reported within a moment of each
  other."
  [^LinkedBlockingQueue queue]
  (let [first-path (.take queue)]
    (Thread/sleep 50)
    (let [more (java.util.ArrayList.)]
      (.drainTo queue more)
      (map io/file (distinct (cons first-path more))))))

(defn- locking*
  "Calls f holding lock."
  [^ReentrantLock lock f]
  (.lock lock)
  (try
    (f)
    (finally
      (.unlock lock))))

(defn- canonical-path [^File f]
  (.getCanonicalPath f))

(defn- watch-opts
  "watch's options: :esm-hmr unless disabled, and with it :esm-repl unless
  disabled, the namespaces required at the REPL are mains too."
  [opts]
  (let [opts (prepare-opts (merge {:esm-hmr true} opts))]
    (cond-> opts
      (and (:esm-hmr opts) (not (false? (:esm-repl opts))))
      (assoc :esm-repl true ::repl-mains (atom #{})))))

(defn watch
  "Builds like build, then recompiles namespaces as their sources change.
  With :watch-events :stdin, the parent process (the Vite plugin) watches
  the directories of the watch-dirs event and writes `changed <path>` lines
  to stdin, otherwise polls :watch-dirs (defaults to the classpath
  directories). Enables :esm-hmr, modules accept their own hot updates when
  served by Vite, and :esm-repl, the REPL evaluates forms in the pages
  running the build (cljs.esm.repl), with an nREPL server under :repl. Until
  a build succeeds, each change builds again: a failed build's output misses
  the namespaces it didn't get to."
  [opts]
  (check-classpath!)
  (let [opts         (watch-opts opts)
        compiler-env (env/default-compiler-env opts)
        dirs         (watch-dirs opts)
        queue        (LinkedBlockingQueue.)
        stdin?       (= :stdin (:watch-events opts))
        ;; recompiles and the REPL's analysis both change the compiler env
        lock         (ReentrantLock.)
        built?       (atom false)
        ;; the build's inputs, for build hooks run by the REPL
        inputs-      (atom [])
        ;; the modification times of the source files compiled: a file the
        ;; REPL compiled (load-file) isn't compiled again when the editor's
        ;; save is reported
        compiled-    (atom {})
        ;; the warnings of the output files, {path [warning]}: like the Vite
        ;; plugin, which holds back hot reloads while there are any
        warnings-    (atom {})
        outstanding! (fn [{:keys [files warnings]}]
                       (swap! warnings-
                         #(reduce (fn [m w] (update m (:output w) (fnil conj []) w))
                            (apply dissoc % files) warnings)))
        build!       (fn []
                       (let [start    (System/nanoTime)
                             warnings (atom [])
                             nses     (binding [ana/*cljs-warning-handlers* (collecting-warnings warnings opts)]
                                        (build opts compiler-env))
                             inputs   (env/with-compiler-env compiler-env
                                        (find-sources (mains opts) opts))]
                         ;; the watcher needs the analysis of the whole build
                         (env/with-compiler-env compiler-env
                           (ensure-analyzed! compiler-env inputs opts))
                         (reset! inputs- inputs)
                         (reset! built? true)
                         (reset! warnings- {})
                         (doto {:type       "compiled"
                                :namespaces (count nses)
                                :warnings   @warnings
                                :ms         (long (/ (- (System/nanoTime) start) 1e6))}
                           outstanding!
                           (->> (event! "compiled")))))
        recompile-changes!
                     (fn [changed res-nses]
                       (let [start  (System/nanoTime)
                             warnings (atom [])
                             ;; the test runner requires the test namespaces there are now
                             runner (test-runner-source opts)
                             before (some-> runner slurp-if-exists)
                             gen    (if (:test-runner opts) (generate-test-runner opts) {})
                             runner-changed (when (and runner (not= before (slurp-if-exists runner)))
                                              [(-> opts :test-runner :ns)])
                             inputs (binding [*generated-sources* gen]
                                      (env/with-compiler-env compiler-env
                                        (find-sources (mains opts) opts)))
                             _      (swap! compiler-env assoc ::namespaces (into #{} (map :ns) inputs))
                             _      (reset! inputs- inputs)
                             macros (keep #(when (re-find #"\.clj[c]?$" (.getName ^File %)) (file-ns %)) changed)
                             _      (doseq [ns macros]
                                      (when (find-ns ns)
                                        (require ns :reload)))
                             cljs   (keep #(when (re-find #"\.clj[sc]$" (.getName ^File %)) (file-ns %)) changed)
                             ;; namespaces newly required by a changed namespace
                             fresh  (remove #(get-in @compiler-env [::ana/namespaces % :name]) (map :ns inputs))
                             done   (env/with-compiler-env compiler-env
                                      (with-bindings (assoc (compiler-bindings opts)
                                                       #'ana/*cljs-warning-handlers* (collecting-warnings warnings opts)
                                                       #'*generated-sources* gen)
                                        (recompile! compiler-env inputs
                                          (distinct (concat fresh cljs res-nses runner-changed
                                                      (macro-dependents compiler-env macros)))
                                          opts)))]
                         (check-js-entries compiler-env opts)
                         (write-constants! compiler-env opts)
                         (install-goog-libs compiler-env opts)
                         (env/with-compiler-env compiler-env
                           (run-hooks! compiler-env inputs opts :watch done))
                         (doto {:type       "compiled"
                                :namespaces (count done)
                                :compiled   (sort done)
                                :files      (map #(.getPath (output-file % opts)) (sort done))
                                :warnings   @warnings
                                :ms         (long (/ (- (System/nanoTime) start) 1e6))}
                           outstanding!
                           (->> (event! "compiled")))))
        compile!     (fn
                       ;; Compiles changed files (sources and macros) and
                       ;; namespaces nses (whose resources changed, or the
                       ;; REPL's), returns the compiled event, or the error
                       ;; event. Files compiled already are skipped, unless
                       ;; :force, nothing to compile is a no-op.
                       [{:keys [files nses force]}]
                       (locking* lock
                         (fn []
                           ;; the watcher's output, also when the REPL compiles
                           (binding [*out* @stdout
                                     *err* @stderr]
                             (let [files (remove #(= (.lastModified ^File %) (get @compiled- (canonical-path %)))
                                           files)]
                               (when (or force (seq files) (seq nses))
                                 (swap! compiled- into
                                   (map (fn [^File f] [(canonical-path f) (.lastModified f)]))
                                   files)
                                 (event! "compiling" {})
                                 (try
                                   (if-not @built?
                                     (build!)
                                     (recompile-changes! files nses))
                                   (catch Throwable e
                                     (doto (assoc (error-data e) :type "error")
                                       (->> (event! "error")))))))))))]
    (when (:exit-with-parent opts)
      (start-stdin-reader! queue))
    (event! "watch-dirs" {:dirs dirs})
    (locking* lock
      (fn []
        (try
          (build!)
          (catch Throwable e
            (event! "error" (error-data e))))))
    (when (:esm-repl opts)
      ((requiring-resolve 'cljs.esm.repl/start!)
       {:compiler-env compiler-env
        :options      opts
        :lock         lock
        :compile!     compile!
        :inputs       inputs-
        :warnings     warnings-
        :watch-dirs   dirs
        :run-hooks!   (fn [nses]
                        (locking* lock
                          (fn []
                            (binding [*out* @stdout
                                      *err* @stderr]
                              (env/with-compiler-env compiler-env
                                (run-hooks! compiler-env @inputs- opts :repl-eval nses))))))
        :output-path  (fn [ns] (util/ns->relpath ns :js))}))
    (loop [files     (when-not stdin? (source-files dirs))
           resources (resource-mtimes (resource-refs compiler-env))]
      (let [[changed files'] (if stdin?
                               [(next-changes queue) nil]
                               (poll-changes dirs files))
            changed    (filter #(re-find #"\.(cljs|cljc|clj)$" (.getName ^File %)) changed)
            refs       (resource-refs compiler-env)
            resources' (resource-mtimes refs)
            ;; namespaces depending on changed resources
            res-nses   (mapcat (fn [[path mtime]]
                                 (when (and (contains? resources path) (not= mtime (get resources path)))
                                   (get refs path)))
                         resources')]
        (when (or (seq changed) (seq res-nses))
          (compile! {:files changed :nses res-nses}))
        (recur files' resources')))))

(defn- deep-merge [& ms]
  (apply merge-with (fn [a b] (if (and (map? a) (map? b)) (deep-merge a b) b)) ms))

(defn load-options
  "Compiler options from command line arguments: @file.edn options files,
  :profile keywords and EDN maps. Profiles are maps under :profiles of the
  files, deep merged over them in order, EDN maps over the result. The
  profile names the build's :mode. Without a profile, default-profile."
  [args default-profile]
  (let [files    (keep #(when (string/starts-with? % "@") (edn/read-string (slurp (subs % 1)))) args)
        maps     (keep #(when (string/starts-with? % "{") (edn/read-string %)) args)
        profiles (or (seq (keep #(when (string/starts-with? % ":") (keyword (subs % 1))) args))
                     [default-profile])
        opts     (apply deep-merge files)]
    (-> (apply deep-merge
          (dissoc opts :profiles)
          (concat (map #(get-in opts [:profiles %]) profiles) maps))
        (assoc :mode (last profiles)))))

(defn -main
  "Usage: clojure -M -m cljs.esm [build|watch] options...

  Options are EDN maps, @file.edn and :profile keywords, see load-options.
  Watch defaults to the :dev profile, build to :release.

    clojure -M -m cljs.esm build '{:main my.app :output-dir \"out\"}'
    clojure -M -m cljs.esm watch @cljs.edn"
  [& args]
  (let [[command args] (if (#{"build" "watch"} (first args))
                         [(first args) (rest args)]
                         ["build" args])]
    (if (= "watch" command)
      (watch (merge {:verbose false} (load-options args :dev)))
      (do
        (build (merge {:verbose true} (load-options args :release)))
        (shutdown-agents)))))
